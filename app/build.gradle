buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.3'
    classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

def butterknifeVersion = '8.3.0' // upgrading to 8.6.0 leads to a
dependencies {
  compile 'com.android.support:appcompat-v7:23.2.1'
  compile 'com.android.support:design:23.2.1'
  compile 'com.android.support:cardview-v7:23.2.1'
  compile 'com.android.support:recyclerview-v7:23.2.1'

  compile 'com.github.QuadFlask:colorpicker:0.0.11'
  compile 'com.google.guava:guava:22.0-android' // should not be needed its already in lib
  compile 'com.squareup.retrofit2:retrofit:2.1.0' // should not be needed its already in lib

  compile "com.jakewharton:butterknife:$butterknifeVersion"
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

  compile project(':lib')
}

// from http://stackoverflow.com/a/22597752
def passwordFromKeychain = { name ->
  def pwd = System.env.passwd
  return pwd ? pwd : "security find-generic-password -s ${name} -a ${name} -w".execute().text.trim();
}

import org.gradle.internal.logging.text.StyledTextOutput;
import org.gradle.internal.logging.text.StyledTextOutputFactory;
import org.gradle.internal.logging.text.StyledTextOutput.Style
def gitVersion = {
  def version = "git describe --dirty".execute().text.trim();
  if (version.indexOf('dirty') != -1) {
    services
      .get(StyledTextOutputFactory)
      .create("dirty")
      .withStyle(Style.Failure)
      .println("ATTENTION dirty build with version: '${version}'")
  }
  return version;
}

android {
  compileSdkVersion 23
  buildToolsVersion '26.0.0'

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 21
    versionCode 3
    versionName gitVersion()
  }

  dexOptions {
    javaMaxHeapSize "4g"
    preDexLibraries = false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  signingConfigs {
    release {
      // just add a new password with dotstar-play-store, dotstar-play-store and password in keychain on osx
      storeFile file("store/release.keystore")
      storePassword passwordFromKeychain("dotstar-play-store")
      keyAlias "dotstar"
      keyPassword passwordFromKeychain("dotstar-play-store")
    }
  }
  playAccountConfigs {
    defaultAccountConfig {
      jsonFile = file('./store/Google Play Android Developer-adb519ba6fad.json')
    }
  }
  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationIdSuffix ".release"
      resValue "string", "app_name", "DotStar"
    }
    debug {
      applicationIdSuffix ".debug"
      resValue "string", "app_name", "DotStar-Debug"
    }
  }
  defaultConfig {
    playAccountConfig = playAccountConfigs.defaultAccountConfig
  }
  lintOptions {
    abortOnError false
  }
}
